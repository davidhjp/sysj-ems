apply plugin: 'java-library'
apply plugin: 'eclipse'
apply plugin: 'application'

defaultTasks 'jar'

def getDate() {
	def date = new Date()
	def formattedDate = date.format('yyyyMMddHHmmss')
	return formattedDate
}

version = '1.0-SNAPSHOT'

buildscript {
	ext.repoUrl = 'https://github.com/hjparker/systemj-release/raw/snapshot/'
	ext.sysj      = [group: 'com.systemjtechnology', name: 'systemj', version:'2.2-9-g17272ea-SNAPSHOT']
	ext.rtbase    = [group: 'com.systemjtechnology', name: 'systemj', version:'2.2-9-g17272ea-SNAPSHOT', classifier: 'sjrt-base']
	ext.rtdesktop = [group: 'com.systemjtechnology', name: 'systemj', version:'2.2-9-g17272ea-SNAPSHOT', classifier: 'sjrt-desktop']

	repositories {
		mavenCentral()
		mavenLocal()
		maven {
			url repoUrl
		}
	}
	dependencies {
		classpath sysj
		classpath rtbase 
		classpath rtdesktop
	}
}

repositories {
	mavenCentral()
	mavenLocal()
	maven {
		url repoUrl
	}
}

dependencies {
	implementation sysj
	implementation rtbase
	implementation rtdesktop
	compile group: 'org.scream3r', name: 'jssc', version: '2.8.0'
	testCompile group: 'junit', name: 'junit', version: '4.12'
}

task SystemJ(type: com.systemj.CompileSystemJ) {
	classpath buildscript.configurations.classpath
	options "--silence": ""
	dependsOn compileJava
}

run {
	def target = 'rpi.xml'
	if (project.hasProperty('target'))
		target = project['target']
	main = 'com.systemj.SystemJRunner'
	args 'src/main/configurations/' + target
	classpath 'build/classes/systemj'
	dependsOn SystemJ
}

jar { 
	if(version.endsWith("-SNAPSHOT"))
		classifier getDate()
}

mainClassName = 'com.systemj.SystemJRunner'
